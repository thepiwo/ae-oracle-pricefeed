// THIS IS NOT SECURITY AUDITED
// DO NEVER USE THIS WITHOUT SECURITY AUDIT FIRST

@compiler >= 7

include "String.aes"
include "Frac.aes"
include "Option.aes"

contract PriceFeedQuery =

  type o = oracle(string, string)
  type oq = oracle_query(string, string)

  type state = o
  entrypoint init(oracle : o) = oracle

  entrypoint queryFee() =
    Oracle.query_fee(state)

  payable stateful entrypoint queryAePrice(currency : string) =
    let fee = queryFee()
    require(Call.value == fee, String.concat("AMOUNT_NOT_EQUAL_FEE_", Int.to_str(fee)))
    require(String.length(currency) == 3, "INCORRECT_CURRENCY_LENGTH")
    require(Oracle.check(state), "ORACLE_CHECK_FAILED")
    Oracle.query(state, currency, fee, RelativeTTL(10), RelativeTTL(10))

  // with the current implementation, the response is expected to be multiplied by 10^18 to provide greater int precision
  entrypoint checkQueryStr(query : oq) : option(string) =
    Oracle.get_answer(state, query)

  // return query response as fractional representation, which can be used for further calculations
  entrypoint checkQueryFrac(query : oq) : option(Frac.frac) =
    switch(checkQueryStr(query))
      None => None
      Some(str) => switch(String.to_int(str))
        None => None
        Some(int) => Some(Frac.make_frac(int, 10^18))
